buildscript {
    dependencies {
        classpath group: "org.spongepowered", name: "mixingradle", version: "0.7-SNAPSHOT"
    }
}

plugins {
    id "maven-publish"
    id 'net.minecraftforge.gradle' version '5.1.+'
    id 'org.parchmentmc.librarian.forgegradle' version '1.+'
}

apply plugin: 'org.spongepowered.mixin'

version = '0.1.0'
group = 'xyz.brassgoggledcoders.reengineeredtooblox'
archivesBaseName = 'reengineered_toolbox'

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

jarJar.enable()

minecraft {
    mappings channel: 'parchment', version: '2022.11.27-1.19.2'

    runs {
        client {
            workingDirectory project.file('run')

            property 'forge.enabledGameTestNamespaces', 'reengineered_toolbox'

            mods {
                reengineered_toolbox {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            property 'forge.enabledGameTestNamespaces', 'reengineered_toolbox'

            mods {
                reengineered_toolbox {
                    source sourceSets.main
                }
            }
        }

        gameTestServer {
            workingDirectory project.file('run')

            property 'forge.enabledGameTestNamespaces', 'reengineered_toolbox'

            mods {
                reengineered_toolbox {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            args '--mod', 'reengineered_toolbox', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                reengineered_toolbox {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources {
    srcDir 'src/generated/resources'
}

repositories {
    mavenLocal()
    maven {
        name = "Registrate"
        url = "https://maven.tterrag.com/"
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.19.2-43.2.0'

    implementation fg.deobf("com.tterrag.registrate:Registrate:MC1.19-1.1.5")
    jarJar(group: 'com.tterrag.registrate', name: 'Registrate', version: "[MC1.19.2,MC1.19.3)")

    implementation fg.deobf("xyz.brassgoggledcoders.shadyskies:ContainerSyncing:1.2.2")
    jarJar(group: 'xyz.brassgoggledcoders.shadyskies', name: 'ContainerSyncing', version: "1.2.2") {
        jarJar.pin(it, "1.2.2")
        jarJar.ranged(it, "[1.2.2, 1.3.0)")
    }

    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "ReEngineered Toolbox",
                "Specification-Vendor"    : "Brass Goggled Coders",
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.jar.archiveVersion,
                "Implementation-Vendor"   : "Brass Goggled Coders",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "MixinConfigs"            : "reengineered_toolbox.mixins.json"
        ])
    }
}

mixin {
    add sourceSets.main, "reengineered_toolbox.refmap.json"

    config "reengineered_toolbox.mixins.json"
}

reobf {
    jarJar { }
}

jar.finalizedBy('reobfJar')
tasks.jarJar.finalizedBy('reobfJarJar')

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/mcmodsrepo"
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
    options.compilerArgs << "-Xmaxerrs" << "10000"
}